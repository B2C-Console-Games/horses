1010 PRINT "BASIC LIBRARY--'HORSES'--18-JUL-70"
1020 REM  *  *  *  *  *  *  *    MAIN PROGRAM  *  *  *  *  *  *  *  *  *  *  *
1030 REM
1040 RANDOM
1050 PRINT"WELCOME TO DATALINE SYSTEMS RACETRACK"
1060 PRINT
1070 PRINT "DO YOU WANT DIRECTIONS";
1080 INPUT X$
1090 IF X$="NO" THEN1150
1100 PRINT"UP TO 10 MAY PLAY.A TABLE OF ODDS WILL BE PRINTED. YOU"
1110 PRINT"MAY BET ANY + AMOUNT UNDER 100000 ON ONE HORSE."
1120 PRINT "DURING THE RACE , A HORSE WILL BE SHOWN BY ITS"
1130 PRINT"NUMBER.THE HORSES RACE DOWN THE PAPER!"
1140 PRINT
1150 PRINT "HOW MANY WANT TO BET";
1160 INPUT C
1170 PRINT "WHEN ? APPEARS,TYPE NAME"
1180 FOR A=1 TO C
1190 INPUT W$(A)
1200 NEXT A
1210 PRINT
1220 PRINT"HORSE"," ","NUMBER","ODDS"
1230 PRINT
1240 LET S(M(I))=0
1250 LET R=0
1260 FOR A=1 TO 8
1270 LET D(A)=INT(10*RND+1)
1280 NEXT A
1290 FOR A=1TO 8
1300 LET R=R+D(A)
1310 NEXT A
1320 LET V$(1)="JOE MAW"
1330 LET V$(2)="L.B.J."
1340 LET V$(3)="MR.WASHBURN"
1350 LET V$(4)="MISS KAREN"
1360 LET V$(5)="JOLLY"
1370 LET V$(6)="FOR Q."
1380 LET V$(7)="JELLY DO NOT"
1390 LET V$(8)="ACID ROCK"
1400 FOR N=1 TO8
1410 PRINT V$(N)," ",N,R/D(N);":1"
1420 NEXT N
1430 PRINT"--------------------------------------------------"
1440 PRINT "PLACE YOUR BETS...HORSE # THEN AMOUNT"
1450 FOR J=1 TO C
1460 PRINT W$(J);
1470 INPUT Q(J),P(J)
1480 IF P(J)<1 THEN 1500
1490 IF P(J)<100000 THEN 1520
1500 PRINT"  YOU CAN'T DO THAT!"
1510 GOTO 1460
1520 NEXT J
1530 PRINT
1540 PRINT"1 2 3 4 5 6 7 8"
1550 PRINT"XXXXSTARTXXXX";
1560 FOR I=1 TON
1570 LET M=I
1580 LET M(I)=M
1590 LET Y(M(I))=INT(100*RND+1)
1600 IF Y(M(I))<10 THEN1690
1610 LET S=INT(R/D(I)+.5)
1620 IF Y(M(I))<S+17 THEN 1710
1630 IF Y(M(I))<S+37 THEN 1730
1640 IF Y(M(I))<S+57 THEN1750
1650 IF Y(M(I))<77+S THEN1770
1660 IF Y(M(I))<S+92 THEN1790
1670 LET Y(M(I))=7
1680 GOTO 1800
1690 LET Y(M(I))=1
1700 GOTO 1800
1710 LET Y(M(I))=2
1720 GOTO 1800
1730 LET Y(M(I))=3
1740 GOTO 1800
1750 LET Y(M(I))=4
1760 GOTO 1800
1770 LET Y(M(I))=5
1780 GOTO 1800
1790 LET Y(M(I))=6
1800 NEXTI
1810 LET M=I
1820 FOR I=1TO8
1830 LET S(M(I))=S(M(I))+Y(M(I))
1840 NEXTI
1850 LET I=1
1860 FOR L=1 TO8
1870 FORI=1TO 8-L
1880 IF S(M(I))<S(M(I+1))THEN 1920
1890 LET H=M(I)
1900 LET M(I)=M(I+1)
1910 LET M(I+1)=H
1920 NEXT I
1930 NEXT L
1940 LET T=S(M(8))
1950 FOR I=1 TO8
1960 LET B=S(M(I))-S(M(I-1))
1970 IF B=0 THEN 2020
1980 FOR A=1 TO B
1990 PRINT
2000 IF S(M(I))>27 THEN2070
2010 NEXT A
2020 PRINT M(I);
2030 NEXT I
2040 FOR A=1 TO 28-T
2050 PRINT
2060 NEXT A
2070 PRINT"XXXXFINISHXXXX"
2080 IF T<28 THEN 1550
2090 PRINT"**************"
2100 PRINT"WINNER IS ";
2110 FOR I=8 TO 1STEP-1
2120 LET F=M(I)
2130 PRINT
2140 PRINT "HORSE NO." F, V$(F)
2150 NEXT I
2160 FOR J=1 TO C
2170 IF Q(J)<>M(8) THEN 2200
2180 LET N=Q(J)
2190 PRINT W$(J);"WINS $";(R/D(N))*P(J)
2200 NEXT J
2210 END
